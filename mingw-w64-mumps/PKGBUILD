# Contributor: Oleg A. Khlybov <fougas@mail.ru>

_realname=mumps

pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=5.3.1
pkgrel=1
arch=('any')
pkgdesc="Sparse direct SLAE solver (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs" "${MINGW_PACKAGE_PREFIX}-gcc-libgfortran" "${MINGW_PACKAGE_PREFIX}-openblas" "${MINGW_PACKAGE_PREFIX}-metis" "${MINGW_PACKAGE_PREFIX}-parmetis" "${MINGW_PACKAGE_PREFIX}-scotch" "${MINGW_PACKAGE_PREFIX}-ptscotch" "${MINGW_PACKAGE_PREFIX}-scalapack")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-gcc-fortran" "${MINGW_PACKAGE_PREFIX}-msmpi" "${MINGW_PACKAGE_PREFIX}-openblas" "${MINGW_PACKAGE_PREFIX}-metis" "${MINGW_PACKAGE_PREFIX}-parmetis" "${MINGW_PACKAGE_PREFIX}-scotch" "${MINGW_PACKAGE_PREFIX}-ptscotch" "${MINGW_PACKAGE_PREFIX}-scalapack")
optdepends=("buildtest: build testing support")
options=('strip' 'staticlibs')
license=('LGPL')
url="http://mumps.enseeiht.fr/"
source=("http://mumps.enseeiht.fr/MUMPS_${pkgver}.tar.gz")
sha256sums=('774fc9411a3ab4704bc907cb7d30090ce6a65b83cde32549c58d3e9f63594e1a')
rootdir=`pwd`

prepare() {
  cd "${srcdir}/${_realname}_${pkgver}"
  for p in ${srcdir}/../*.patch; do
	patch -p1 < $p
  done
}

_seq_pc="metis esmumps openblas"
_mpi_pc="scalapack parmetis ptesmumps openblas msmpi"

build() {
  cd "${srcdir}/${_realname}_${pkgver}"
  for mod in PAR SEQ; do
    cp Makefile.inc.${mod} Makefile.inc
    make -j1 clean all
  done
  cd lib
  rm -rf libmumps-*.a
  cp ../libseq/libmpi_seq.a .
  strip -S *.a # thin archives require the constituent archives to be stripped beforehand
  for x in c z s d; do
    ar crsT libmumps-${x}so.a lib${x}mumps_seq.a libmumps_common_seq.a libpord_seq.a libmpi_seq.a
    ar crsT libmumps-${x}mo.a lib${x}mumps_mpi.a libmumps_common_mpi.a libpord_mpi.a
    gfortran -shared -Wl,--enable-auto-import -Wl,--export-all-symbols -o libmumps-${x}so.dll -Wl,--out-implib,libmumps-${x}so.dll.a -Wl,--whole-archive libmumps-${x}so.a -Wl,--no-whole-archive $(pkg-config ${_seq_pc} --libs)
    mpif90 -shared -Wl,--enable-auto-import -Wl,--export-all-symbols -o libmumps-${x}mo.dll -Wl,--out-implib,libmumps-${x}mo.dll.a -Wl,--whole-archive libmumps-${x}mo.a -Wl,--no-whole-archive $(pkg-config ${_mpi_pc} --libs)
  done
}

package() {
  cd "$srcdir/${_realname}_${pkgver}"
  mkdir -p ${pkgdir}${MINGW_PREFIX}/{bin,lib,include/mumps/mpi_seq} ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig ${pkgdir}${MINGW_PREFIX}/share/tests/mumps
  (
    cd libseq
    install -m644 mpi*.h "${pkgdir}${MINGW_PREFIX}/include/mumps/mpi_seq"
  )
  (
    cd lib
    install -m644 *.a "${pkgdir}${MINGW_PREFIX}/lib"
    install -m644 *.dll "${pkgdir}${MINGW_PREFIX}/bin"
  )
  (
    cd include
    install -m644 *.h "${pkgdir}${MINGW_PREFIX}/include/mumps"
  )
  for x in c z s d; do
    echo "
      prefix=${MINGW_PREFIX}
      libdir=\${prefix}/lib
      includedir=\${prefix}/include/mumps
      Name: ${_realname}
      URL: ${url}
      Version: ${pkgver}
      Description: Sequential version of the sparse direct SLAE solver (mingw-w64)
      Requires.private: ${_seq_pc}
      Cflags: -I\${includedir} -I\${includedir}/mpi_seq
      Libs.private: -lmumps-${x}so -lgfortran -lquadmath
      Libs: -L\${libdir} -lmumps-${x}so
    " | sed '/^\s*$/d;s/^\s*//' > "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/mumps-${x}so.pc"
    echo "
      prefix=${MINGW_PREFIX}
      libdir=\${prefix}/lib
      includedir=\${prefix}/include/mumps
      Name: ${_realname}
      URL: ${url}
      Version: ${pkgver}
      Description: MPI-aware parallel version of the sparse direct SLAE solver (mingw-w64)
      Cflags: -I\${includedir}
      Requires.private: ${_mpi_pc}
      Libs.private: -lmumps-${x}mo -lgfortran -lquadmath
      Libs: -L\${libdir} -lmumps-${x}mo
    " | sed '/^\s*$/d;s/^\s*//' > "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/mumps-${x}mo.pc"
  done
  (
    cd examples
    cp c_example.c ?simpletest.F input_simpletest_* ${pkgdir}${MINGW_PREFIX}/share/tests/mumps
    cd $rootdir
    cp -R tests ${pkgdir}${MINGW_PREFIX}/share
  )
}


