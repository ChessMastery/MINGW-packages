# Contributor: Oleg A. Khlybov <fougas@mail.ru>

_realname=scalapack

pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.0.2
pkgrel=1
arch=('any')
pkgdesc="Distributed version of Linear Algebra PACKage (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs" "${MINGW_PACKAGE_PREFIX}-gcc-libgfortran" "${MINGW_PACKAGE_PREFIX}-openblas") # No way to introduce the required msmpi runtime dependency
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-gcc-fortran" "${MINGW_PACKAGE_PREFIX}-openblas" "${MINGW_PACKAGE_PREFIX}-msmpi")
options=('strip' 'staticlibs')
license=('LGPL')
url="http://www.netlib.org/scalapack"
source=("http://www.netlib.org/${_realname}/${_realname}-${pkgver}.tgz")
sha256sums=('0c74aeae690fe5ee4db7926f49c5d0bb69ce09eea75beb915e00bba07530395c')


# Build issues:
# - Parallel build broken
# - Introduced time delays before ar


prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  for p in ${srcdir}/../*.patch; do
	patch -p1 < $p
  done
}


build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make -j1 lib
  ranlib lib${_realname}.a
  mpif90 -shared -o lib${_realname}.dll -Wl,--enable-auto-import -Wl,--export-all-symbols -Wl,--output-def,lib${_realname}.def -Wl,--out-implib,lib${_realname}.dll.a -Wl,--whole-archive lib${_realname}.a -Wl,--no-whole-archive `pkg-config openblas --libs`
}

_pc="
prefix=${MINGW_PREFIX}
libdir=\${prefix}/lib
includedir=\${prefix}/include
Name: ${_realname}
URL: ${url}
Version: ${pkgver}
Description: ${pkgdesc}
Requires.private: openblas msmpi
Libs.private: -l${_realname} -lgfortran
Libs: -L\${libdir} -l${_realname}
"

package() {
  cd "$srcdir/${_realname}-${pkgver}"
  mkdir -p ${pkgdir}${MINGW_PREFIX}/{bin,lib}
  mkdir -p "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig"
  install -m644 lib${_realname}*.a "${pkgdir}${MINGW_PREFIX}/lib"
  install -m644 lib${_realname}*.dll "${pkgdir}${MINGW_PREFIX}/bin"
  echo "${_pc}" > "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/${_realname}.pc"
}
