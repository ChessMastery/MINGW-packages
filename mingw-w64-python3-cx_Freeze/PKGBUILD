# Maintainer: Frode Solheim <frode@fs-uae.net>
# Contributor: Duong Pham <dthpham@gmail.com>
# Contributor: Lara Maia <dev@lara.click>

_realname=cx_Freeze
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-cx_Freeze")
pkgver=6.0
pkgrel=1
pkgdesc="Python package for freezing scripts into executables (mingw-w64)"
arch=('any')
license=('PSF')
url="https://cx-freeze.sourceforge.io/"
depends=("${MINGW_PACKAGE_PREFIX}-python3"
         "${MINGW_PACKAGE_PREFIX}-python3-six")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools")
options=('staticlibs' 'strip' '!debug')
source=(cx_Freeze-${pkgver}.tar.gz::https://github.com/anthony-tuininga/cx_Freeze/archive/${pkgver}.tar.gz)
sha256sums=('35185441a388032dc3ecc59e697a16ae00929316d0043aed846dd2e434222580')

prepare() {
  rm -Rf python3-cx_Freeze-${pkgver}-${CARCH}
  cp -a cx_Freeze-${pkgver} python3-cx_Freeze-${pkgver}-${CARCH}
}

build() {
  echo "Building for Python 3"
  cd python3-cx_Freeze-${pkgver}-${CARCH}
  ${MINGW_PREFIX}/bin/python3 setup.py build
}

package() {
  local _mingw_prefix=$(cygpath -am ${MINGW_PREFIX})
  cd python3-cx_Freeze-${pkgver}-${CARCH}
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1

  # fix python command in files
  pushd "${pkgdir}${MINGW_PREFIX}"/bin > /dev/null
  for filename in cxfreeze cxfreeze-quickstart; do
    sed -e "s|${_mingw_prefix}/bin/|/usr/bin/env |g" -i ${filename}
  done
  popd > /dev/null
}
