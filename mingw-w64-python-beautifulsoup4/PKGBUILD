#Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=beautifulsoup4
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-beautifulsoup4" "${MINGW_PACKAGE_PREFIX}-python2-beautifulsoup4")
pkgver=4.8.1
pkgrel=1
pkgdesc="A Python HTML/XML parser designed for quick turnaround projects like screen-scraping (mingw-w64)"
arch=('any')
url="https://www.crummy.com/software/BeautifulSoup/index.html"
license=('PSF')
makedepends=("${MINGW_PACKAGE_PREFIX}-python2-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python2-soupsieve"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-soupsieve")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python2-pytest"
              "${MINGW_PACKAGE_PREFIX}-python3-pytest")
source=(#"https://www.crummy.com/software/BeautifulSoup/bs4/download/${pkgver%.*}/${_realname#*-}-${pkgver}.tar.gz"
        https://files.pythonhosted.org/packages/86/cd/495c68f0536dcd25f016e006731ba7be72e072280305ec52590012c1e6f2/beautifulsoup4-${pkgver}.tar.gz)
sha256sums=('6135db2ba678168c07950f9a16c4031822c6f4aec75a65e0a97bc5ca09789931')
noextract=("${_realname}-${pkgver}.tar.gz")

prepare() {
  cd "${srcdir}"
  [[ -d ${_realname}-${pkgver} ]] && rm -rf ${_realname}-${pkgver}
  tar -xzf "${_realname}-${pkgver}.tar.gz" -C ${srcdir} || true

  cd "${srcdir}"
  for pver in {2,3}; do 
    rm -rf python${pver}-build-${CARCH} | true
    cp -r "${_realname}-${pkgver}" "python${pver}-build-${CARCH}"
  done
}

build() {
  for pver in {2,3}; do  
    msg "Python ${pver} build for ${CARCH}"  
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/python${pver} setup.py build
  done 
}

check() {
  msg "Python 2 test for ${CARCH}"
  cd "${srcdir}/python2-build-${CARCH}"
  ${MINGW_PREFIX}/bin/py.test2

  msg "Python 3 test for ${CARCH}"
  cd "${srcdir}/python3-build-${CARCH}"
  ${MINGW_PREFIX}/bin/2to3 -w bs4
  ${MINGW_PREFIX}/bin/py.test
}

package_python3-beautifulsoup4() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3"
           "${MINGW_PACKAGE_PREFIX}-python3-soupsieve")
  optdepends=("${MINGW_PACKAGE_PREFIX}-python3-chardet: to autodetect character encodings"
              "${MINGW_PACKAGE_PREFIX}-python3-lxml: alternative HTML parser"
              "${MINGW_PACKAGE_PREFIX}-python3-html5lib: alternative HTML parser")

 cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-beautifulsoup4() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2"
           "${MINGW_PACKAGE_PREFIX}-python2-soupsieve")
  optdepends=("${MINGW_PACKAGE_PREFIX}-python2-chardet: to autodetect character encodings"
              "${MINGW_PACKAGE_PREFIX}-python2-lxml: alternative HTML parser"
              "${MINGW_PACKAGE_PREFIX}-python2-html5lib: alternative HTML parser")

  cd "${srcdir}/python2-build-${CARCH}"

  rm -f ./buildutils/include_win32/stdint.h
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
}

package_mingw-w64-i686-python2-beautifulsoup4() {
  package_python2-beautifulsoup4
}

package_mingw-w64-i686-python3-beautifulsoup4() {
  package_python3-beautifulsoup4
}

package_mingw-w64-x86_64-python2-beautifulsoup4() {
  package_python2-beautifulsoup4
}

package_mingw-w64-x86_64-python3-beautifulsoup4() {
  package_python3-beautifulsoup4
}

