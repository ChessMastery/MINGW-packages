# Maintainer: Lars Kanis <lars@greiz-reinsdorf.de>

_realname=libvips
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=8.8.3
pkgrel=2
pkgdesc="A fast image processing library with low memory needs (mingw-w64)"
arch=('any')
url="https://libvips.github.io/libvips/"
license=("LGPL-2.1")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
             "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-cairo"
         "${MINGW_PACKAGE_PREFIX}-cfitsio"
         "${MINGW_PACKAGE_PREFIX}-fftw"
         "${MINGW_PACKAGE_PREFIX}-giflib"
         "${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-gobject-introspection-runtime"
         "${MINGW_PACKAGE_PREFIX}-imagemagick"
         "${MINGW_PACKAGE_PREFIX}-lcms2"
         "${MINGW_PACKAGE_PREFIX}-libexif"
         "${MINGW_PACKAGE_PREFIX}-libgsf"
         "${MINGW_PACKAGE_PREFIX}-libimagequant"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-librsvg"
         "${MINGW_PACKAGE_PREFIX}-libtiff"
         "${MINGW_PACKAGE_PREFIX}-libwebp"
#         "${MINGW_PACKAGE_PREFIX}-matio"   Temporary disabled by --without-matio due to https://github.com/msys2/MINGW-packages/issues/5758
         "${MINGW_PACKAGE_PREFIX}-opencl-icd-git"
         "${MINGW_PACKAGE_PREFIX}-openexr"
#         "${MINGW_PACKAGE_PREFIX}-orc"    Temporary disabled by --without-orc due to https://github.com/libvips/libvips/issues/1426
         "${MINGW_PACKAGE_PREFIX}-pango"
         "${MINGW_PACKAGE_PREFIX}-poppler")
options=('staticlibs' 'strip')
source=("https://github.com/libvips/libvips/releases/download/v${pkgver}/vips-${pkgver}.tar.gz"
        "0001-Fix-dependency-to-introspect-program-on-Windows-MING.patch")
sha256sums=('c5e4dd5a5c6a777c129037d19ca606769b3f1d405fcc9c8eeda906a61491f790'
            'b58454a9c50145187cf69f018351ffe93a8a49809a44435f175cc7e109507cd1')


prepare() {
  cd "${srcdir}"/vips-${pkgver}
  patch -p1 -i ${srcdir}/0001-Fix-dependency-to-introspect-program-on-Windows-MING.patch

  autoreconf -if
}

build() {
  [[ -d "${srcdir}/build-${CARCH}" ]] && rm -rf "${srcdir}/build-${CARCH}"
  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"
  ../vips-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-shared \
    --disable-static \
    --without-matio \
    --without-openslide \
    --without-orc

  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="${pkgdir}" install
}
