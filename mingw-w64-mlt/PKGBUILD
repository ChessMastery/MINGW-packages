# Maintainer: Artem Konoplin <konoplin@gmail.com>

_realname=mlt
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=6.20.0
pkgrel=1
pkgdesc="An open source multimedia framework (mingw-w64)"
arch=('any')
url="https://www.mltframework.org"
license=('GPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-dlfcn" 
             "${MINGW_PACKAGE_PREFIX}-SDL2"
             "${MINGW_PACKAGE_PREFIX}-fftw"
             "${MINGW_PACKAGE_PREFIX}-ffmpeg")
#options=('strip' 'staticlibs')
source=("https://github.com/mltframework/mlt/archive/v$pkgver.tar.gz"
        "010-mlt-6.20.0-fix-configure-paths.patch")
sha256sums=('ab211e27c06c0688f9cbe2d74dc0623624ef75ea4f94eea915cdc313196be2dd'
            'a039599ffbf976e9bb60da2be33876f7cac19a9540e7a0576aec75bc9831773e')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/010-mlt-6.20.0-fix-configure-paths.patch"
}

build() {
  cd mlt-$pkgver

  ./configure \
    --avformat-swscale \
    --enable-gpl \
    --enable-gpl3 \
    --enable-opencv \
    --disable-gtk2 \
    --prefix=${MINGW_PREFIX} \
    --target-arch=${MSYSTEM_CARCH}
  make -j$(nproc)
}

package() {
  cd mlt-$pkgver
  make DESTDIR="$pkgdir" install
}
